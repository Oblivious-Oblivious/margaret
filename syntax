first_unit:
    translation_unit EOF

translation_unit:
    assignment_chain keyword_message

assignment_chain:
    IDENTIFIER '='
  | IDENTIFIER '=' assignment_chain
  | ε

keyword_message:
    binary_message keyword_selector_chain

keyword_selector_chain:
    IDENTIFIER ':' binary_message
  | IDENTIFIER ':' binary_message keyword_selector_chain
  | ε

binary_message:
    unary_message binary_selector_chain

binary_selector_chain:
    MESSAGE_SYMBOL unary_message
  | MESSAGE_SYMBOL unary_message binary_selector_chain
  | ε

unary_message:
    lhs_message unary_selector_chain

unary_selector_chain:
    IDENTIFIER
  | IDENTIFIER unary_selector_chain
  | ε

lhs_message:
    lhs_selector literal

lhs_selector:
    LHS_OPERATOR
  | ε

literal:
    '$' 'nil'
  | '$' 'false'
  | '$' 'true'
  | ':' ':' IDENTIFIER
  | INTEGER
  | FLOAT
  | STRING
  | IDENTIFIER
  | '(' group_items_or_empty ')'
  | '[' bit_literal_list ']'
  | '[' association_list ']'
  | '[' group_items_or_empty ']'
  | '{' param_list_or_empty group_items_or_empty '}'
  | '#' literal method_definition
  | '#' method_definition

bit_literal_list:
    bit_literal
  | bit_literal ',' bit_literal_list

bit_literal:
    IDENTIFIER ':' ':' INTEGER
  | INTEGER ':' ':' INTEGER

association_list:
    association
  | association ',' association_list

association:
    IDENTIFIER ':' translation_unit
  | STRING ':' translation_unit

param_list_or_empty:
    IDENTIFIER '|'
  | IDENTIFIER ',' param_list_or_empty
  | ε

group_items_or_empty:
    translation_unit
  | translation_unit ',' group_items_or_empty
  | ε

method_definition:
    keyword_list '=>' translation_unit
  | MESSAGE_SYMBOL literal '=>' translation_unit
  | IDENTIFIER '=>' translation_unit

keyword_list:
    IDENTIFIER ':' literal
  | IDENTIFIER ':' literal keyword_list

FLOAT:                ^\d+(_\d+)*\.\d+(_\d+)*
INTEGER:              ^(?:0[bBoOxX][\da-fA-F_]+|0|[1-9][\d_]*)
STRING:               ^(['"]).*?\1

IDENTIFIER:           ^[@$]?[a-zA-Z_][a-zA-Z0-9_]*
MESSAGE_SYMBOL:       ^[!?+\-*/\\~<>=%|&^;.`]+
SYNTAX_SYMBOL:        ^[()\[\]{},:#]

LHS_OPERATOR:         ^[+-!~]

EOF:                  ends parsing
