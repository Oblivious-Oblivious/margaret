$Margaret _PRIM_BIND_METHOD: (#-- method => _PRIM_BIND_METHOD: method to: @self) to: @self,
$Margaret -- #-- message => -- method to: @self,

$Margaret -- #self => @self,
$Margaret -- #super => @super,

$Margaret -- #messages => _PRIM_MESSAGES: @self,
$Margaret -- #object_id: object => _PRIM_OBJECT_ID: object,
$Margaret -- #to_string => _PRIM_TO_STRING: @self,
$Margaret -- #== other => _PRIM_EQUALS?: @self with: other,
$Margaret -- #dnu: message_name => puts: (_PRIM_DNU: @self msg: message_name),
$Margaret -- #clone: name => _PRIM_CLONE_OBJECT: @self with_name: name,

$Numeric -- #== other => _PRIM_EQUALS_NUMERIC?: @self with: other,
$Numeric -- #+ other => _PRIM_ADD: @self with: other,
$Numeric -- #- other => _PRIM_SUB: @self with: other,
$Numeric -- #* other => _PRIM_MUL: @self with: other,
$Numeric -- #/ other => _PRIM_DIV: @self with: other,
$Numeric -- #abs => _PRIM_ABS: @self,
$Numeric -- #< other => _PRIM_LESS: @self than: other,
$Numeric -- #> other => _PRIM_GREATER: @self than: other,
$Numeric -- #<= other => _PRIM_LESS_OR_EQUALS: @self than: other,
$Numeric -- #>= other => _PRIM_GREATER_OR_EQUALS: @self than: other,

$Integer -- #incr => _PRIM_INCR: @self,
$Integer -- #decr => _PRIM_DECR: @self,
$Integer -- #double => _PRIM_DOUBLE: @self,

$true -- #not => $false,
$false -- #not => $true,

$Numeric -- #!= other => (@self == other) not,

$true -- #if_false: proc => $nil,
$true -- #if_false: false_proc if_true: true_proc => true_proc call,
$true -- #if_true: proc => proc call,
$true -- #if_true: true_proc if_false: false_proc => true_proc call,

$false -- #if_false: proc => proc call,
$false -- #if_false: false_proc if_true: true_proc => false_proc call,
$false -- #if_true: proc => $nil,
$false -- #if_true: true_proc if_false: false_proc => false_proc call,

$Margaret -- #if: condition then: proc => {
  condition
    if_true: proc
    if_false: {},
},
$Margaret -- #if: condition then: true_proc else: false_proc => {
  condition
    if_true: true_proc
    if_false: false_proc,
},
